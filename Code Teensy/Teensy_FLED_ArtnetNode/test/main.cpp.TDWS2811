/* MIT License

  Original concept:
  Copyright (c) 2020 Ward Ramsdell

  Extensively revised by:
  Copyright (c) 2021 Arn Mulligan

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
*/

/*
   Move a colored stripe back and forth along the pixel array to demonstrate
   multi-channel capability.
*/

#include <TDWS2811.h>
#include <SPI.h>
#include <NativeEthernet.h>
#include <NativeEthernetUdp.h>

using namespace TDWS28XX;

const uint8_t NumberOfChannels = 6;            // number of shift register outputs in use (max 32)
const uint16_t NumberOfPixelsPerChannel = 104; // total LED pixels connected to a shift register output

const Color BaseColor = grbw(0, 64, 0, 10);
const Color ForeColor = grbw(64, 20, 64, 150);

// PixelBuffer<NumberOfPixelsPerChannel, TRICOLOR, DOUBLE_BUFFER> pb; // store large buffer in RAM1
DMAMEM PixelBuffer<NumberOfPixelsPerChannel, QUADCOLOR, DOUBLE_BUFFER> pb; // store large buffer in RAM2

PixelDriver pd(pb);
uint16_t curPosition = 0; // current position of colored stripe
bool forward = true;      // direction of movement of the stripe

void paintBackground(const Color &color)
{
  for (uint8_t i = 0; i < NumberOfChannels; ++i)
  {
    for (uint16_t j = 0; j < NumberOfPixelsPerChannel; ++j)
    {
      pd.setInactivePixel(i, j, color);
    }
  }
}

void setup()
{
  Serial.begin(115200);
  randomSeed(333);
  Serial.println("Started");
  if (!pd.begin(FLEXIO1, {2, 3, 4}))
  {
    Serial.println("configuration error");
    for (;;)
      ;
  }

  // some configuration
  for (uint8_t i = 0; i < NumberOfChannels; ++i)
  {
    pd.setChannelType(i, GRBW);
  }

  // initialise the inactive buffer with background color
  paintBackground(BaseColor);

  // set each first pixel to a different color to draw a stripe and to
  // make each pixel strip visually unique
  for (uint16_t i = 0; i < NumberOfChannels; ++i)
  {
    Color color = grb(
        (uint8_t)random(ForeColor.GRB.green + 1),
        (uint8_t)random(ForeColor.GRB.red + 1),
        (uint8_t)random(ForeColor.GRB.blue + 1));
    pd.setInactivePixel(i, 0, color);
  }

  // swap active buffer for inactive buffer thus updating the display
  pd.flipBuffers();

  // initialise the formerly active buffer
  paintBackground(BaseColor);
}

void loop()
{
  delay(50);

  const uint16_t newPosition = forward ? curPosition + 1 : curPosition - 1;

  // draw stripe in new position
  for (uint16_t i = 0; i < NumberOfChannels; ++i)
  {
    pd.setInactivePixel(i, newPosition, pd.getActivePixel(i, curPosition));
  }
    Serial.print("newPosition: ");
    Serial.println(newPosition);
  // display the new stripe
  pd.flipBuffers();

  // erase old stripe
  for (uint16_t i = 0; i < NumberOfChannels; ++i)
  {
    pd.setInactivePixel(i, curPosition, BaseColor);
  }

  // housekeeping
  curPosition = newPosition;
  if (curPosition == 0 || curPosition + 1 == NumberOfPixelsPerChannel)
  {
    forward = !forward;
    Serial.print("curPosition: ");
    Serial.println(curPosition);
  }
}